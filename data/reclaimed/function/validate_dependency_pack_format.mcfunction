# Check if dependencies are loaded
execute if function reclaimed:has_loaded_dependencies run return fail
# Otherwise, print to user what is missing...
tellraw @s {text: "[ERROR]:", color: "dark_red"}
## Aspects is too low of a version
execute if score #reclaimed aspects.pack_format.dependency.min = #reclaimed aspects.pack_format.dependency.max if score #aspects aspects.pack_format < #reclaimed aspects.pack_format.dependency.min run return run tellraw @s {"translate": "addon.dependency_missmatch.min", "fallback": "Addon \"%1$s\" depends on an older version of \"%2$s\"!\nReplace \"%2$s\" with versions that are within pack formats %3$s.", "color": "red", "with": [{"translate": "reclaimed", "color": "#b741b6"}, {"translate": "aspects", "color": "#f88379"}, {"score": {"name": "#reclaimed", "objective": "aspects.pack_format.dependency.min"}, "color": "gold"}, {"score": {"name": "#aspects", "objective": "aspects.pack_format"}, "color": "gold"}]}
execute unless score #reclaimed aspects.pack_format.dependency.min = #reclaimed aspects.pack_format.dependency.max if score #aspects aspects.pack_format < #reclaimed aspects.pack_format.dependency.min run return run tellraw @s {"translate": "addon.dependency_missmatch.min", "fallback": "Addon \"%1$s\" depends on an older version of \"%2$s\"!\nReplace \"%2$s\" with versions that are within pack formats %3$s.", "color": "red", "with": [{"translate": "reclaimed", "color": "#b741b6"}, {"translate": "aspects", "color": "#f88379"}, {"text": "", "color": "gold", extra: [{"score": {"name": "#reclaimed", "objective": "aspects.pack_format.dependency.min"}}, "-", {"score": {"name": "#reclaimed", "objective": "aspects.pack_format.dependency.max"}}]}, {"score": {"name": "#aspects", "objective": "aspects.pack_format"}, "color": "gold"}]}
## Aspects is too new of a version
execute if score #reclaimed aspects.pack_format.dependency.min = #reclaimed aspects.pack_format.dependency.max if score #aspects aspects.pack_format > #reclaimed aspects.pack_format.dependency.max run return run tellraw @s {"translate": "addon.dependency_missmatch.max", "fallback": "Addon \"%1$s\" depends on a newer version of \"%2$s\"!\nReplace \"%2$s\" with versions that are within pack formats %3$s.", "color": "red", "with": [{"translate": "reclaimed", "color": "#b741b6"}, {"translate": "aspects", "color": "#f88379"}, {"score": {"name": "#reclaimed", "objective": "aspects.pack_format.dependency.max"}, "color": "gold"}, {"score": {"name": "#aspects", "objective": "aspects.pack_format"}, "color": "gold"}]}
execute unless score #reclaimed aspects.pack_format.dependency.min = #reclaimed aspects.pack_format.dependency.max if score #aspects aspects.pack_format > #reclaimed aspects.pack_format.dependency.max run return run tellraw @s {"translate": "addon.dependency_missmatch.max", "fallback": "Addon \"%1$s\" depends on a newer version of \"%2$s\"!\nReplace \"%2$s\" with versions that are within pack formats %3$s.", "color": "red", "with": [{"translate": "reclaimed", "color": "#b741b6"}, {"translate": "aspects", "color": "#f88379"}, {"text": "", "color": "gold", extra: [{"score": {"name": "#reclaimed", "objective": "aspects.pack_format.dependency.min"}}, "-", {"score": {"name": "#reclaimed", "objective": "aspects.pack_format.dependency.max"}}]}, {"score": {"name": "#aspects", "objective": "aspects.pack_format"}, "color": "gold"}]}
## If version is not what triggered it, assume a dependency is missing entirely
execute if score #reclaimed aspects.pack_format.dependency.min = #reclaimed aspects.pack_format.dependency.max run return run tellraw @s {"translate": "addon.dependency_missmatch.missing", "fallback": "Addon \"%1$s\" depends on \"%2$s\"!\nInstall \"%2$s\" with versions that are within pack formats %3$s.", "color": "red", "with": [{"translate": "reclaimed", "color": "#b741b6"}, {"translate": "aspects", "color": "#f88379"}, {"score": {"name": "#reclaimed", "objective": "aspects.pack_format.dependency.max"}, "color": "gold"}]}
execute unless score #reclaimed aspects.pack_format.dependency.min = #reclaimed aspects.pack_format.dependency.max run return run tellraw @s {"translate": "addon.dependency_missmatch.missing", "fallback": "Addon \"%1$s\" depends on \"%2$s\"!\nInstall \"%2$s\" with versions that are within pack formats %3$s.", "color": "red", "with": [{"translate": "reclaimed", "color": "#b741b6"}, {"translate": "aspects", "color": "#f88379"}, {"text": "", "color": "gold", extra: [{"score": {"name": "#reclaimed", "objective": "aspects.pack_format.dependency.min"}}, "-", {"score": {"name": "#reclaimed", "objective": "aspects.pack_format.dependency.max"}}]}]}
